set(MODULE_NAME "QcMuonChambers")

# ---- Files ----

set(SRCS
  src/Mapping.cxx
  src/Decoding.cxx
  src/GlobalHistogram.cxx
  src/PedestalsTask.cxx
  src/PhysicsTask.cxx
  src/PhysicsTaskDigits.cxx
  src/PhysicsTaskPreClusters.cxx
  src/PedestalsCheck.cxx
  src/PhysicsCheck.cxx
  src/MCHPostProcessing.cxx
  src/TH1MCHReductor.cxx
  src/TH2MCHReductor.cxx
  src/GlobalHistogramReductor.cxx
)

set(HEADERS
  include/MCH/Mapping.h
  include/MCH/Decoding.h
  include/MCH/GlobalHistogram.h
  include/MCH/PedestalsTask.h
  include/MCH/PhysicsTask.h
  include/MCH/PhysicsTaskDigits.h
  include/MCH/PhysicsTaskPreClusters.h
  include/MCH/PedestalsCheck.h
  include/MCH/PhysicsCheck.h
  include/MCH/MCHPostProcessing.h
  include/MCH/TH1MCHReductor.h
  include/MCH/TH2MCHReductor.h
  include/MCH/GlobalHistogramReductor.h
  include/MCH/CustomMergeableTH2Quotient.h
)

# ---- Library ----

add_library(${MODULE_NAME} SHARED ${SRCS})

target_include_directories(
  ${MODULE_NAME}
  PUBLIC $<INSTALL_INTERFACE:include> $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
  PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/src
)

target_link_libraries(${MODULE_NAME} PUBLIC QualityControl O2::CommonDataFormat O2::GPUCommon 
        $<TARGET_NAME_IF_EXISTS:O2::MCHMappingFactory> O2::MCHMappingImpl3 O2::MCHMappingSegContour O2::MCHRawDecoder O2::MCHPreClustering O2::Mergers)

target_compile_definitions(${MODULE_NAME} PRIVATE $<$<TARGET_EXISTS:O2::MCHMappingFactory>:MCH_HAS_MAPPING_FACTORY>)

install(
        TARGETS ${MODULE_NAME}
        LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
        ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
        RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

# ---- ROOT dictionary ----

add_root_dictionary(${MODULE_NAME}
                    HEADERS include/MCH/Mapping.h
                            include/MCH/Decoding.h
                            include/MCH/GlobalHistogram.h
                            include/MCH/PedestalsTask.h
                            include/MCH/PhysicsTask.h
                            include/MCH/PhysicsTaskDigits.h
                            include/MCH/PhysicsTaskPreClusters.h
                            include/MCH/PedestalsCheck.h
                            include/MCH/PhysicsCheck.h
                            include/MCH/sampa_header.h
                            include/MCH/MCHPostProcessing.h
                            include/MCH/TH1MCHReductor.h
                            include/MCH/TH2MCHReductor.h
                            include/MCH/GlobalHistogramReductor.h
                            include/MCH/CustomMergeableTH2Quotient.h
                    LINKDEF include/MCH/LinkDef.h)
                    
# ---- Executables ----

set(EXE_SRCS
     src/TestMerging.cxx)
set(EXE_NAMES
     o2-qc-run-test-merging)

list(LENGTH EXE_SRCS count)
math(EXPR count "${count}-1")
foreach(i RANGE ${count})
  list(GET EXE_SRCS ${i} src)
  list(GET EXE_NAMES ${i} name)
  add_executable(${name} ${src})
  target_link_libraries(${name} PRIVATE QualityControl QcMuonChambers)
endforeach()

install(
  TARGETS o2-qc-run-test-merging
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

# ---- Tests ----

set(
  TEST_SRCS
)

foreach(test ${TEST_SRCS})
  get_filename_component(test_name ${test} NAME)
  string(REGEX REPLACE ".cxx" "" test_name ${test_name})

  add_executable(${test_name} ${test})
  target_link_libraries(${test_name} PRIVATE ${MODULE_NAME} Boost::unit_test_framework)
  add_test(NAME ${test_name} COMMAND ${test_name})
  set_tests_properties(${test_name} PROPERTIES TIMEOUT 60)
endforeach()
